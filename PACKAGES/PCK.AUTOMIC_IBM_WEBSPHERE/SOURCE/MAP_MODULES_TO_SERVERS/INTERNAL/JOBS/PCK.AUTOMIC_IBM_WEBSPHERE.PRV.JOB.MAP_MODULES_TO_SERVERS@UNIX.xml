<?xml version="1.0" encoding="UTF-8"?>
<jobs>
   <metadata>
      <row>
         <version>12.3.1</version>
      </row>
   </metadata>
   <general_attributes>
      <row>
         <minimum_ae_version>11.2</minimum_ae_version>
         <auto_deactivation>F</auto_deactivation>
         <child_flags>00000000000000000000000000000000</child_flags>
         <deactivation_condition>ANY_OK</deactivation_condition>
         <platform>UNIX</platform>
         <last_runtimes>AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA</last_runtimes>
         <max_parallel_action>1</max_parallel_action>
         <mrt_time>000000</mrt_time>
         <name>PCK.AUTOMIC_IBM_WEBSPHERE.PRV.JOB.MAP_MODULES_TO_SERVERS@UNIX</name>
         <type>JOBS</type>
         <inherit_output_filter>N</inherit_output_filter>
         <queue>CLIENT_QUEUE</queue>
         <description>Map Modules To Servers</description>
         <versioning_id>-877078310</versioning_id>
      </row>
   </general_attributes>
   <scripts>
      <row>
         <process><![CDATA[:SET &CHECK# = 0
: IF &UC4RB_WAS_FILE_PATH# <> ""
:   SET &CHECK# = &CHECK# + 1
: ENDIF
: IF &UC4RB_WAS_APPLICATION_NAME# <> ""
:   SET &CHECK# = &CHECK# + 1
: ENDIF
:IF &CHECK# = 0
: STOP MSG, 50, "Either File Path or Application Name must be provided."
:ENDIF
:IF &CHECK# = 2
: STOP MSG, 50, "Both File Path or Application Name should not be provided."
:ENDIF

: IF &UC4RB_WAS_MODULE_OPERATION# = "Install"
:   IF &UC4RB_WAS_FILE_PATH# = ""
:     STOP MSG, 50, "File Path must be provided for Install operation."
:   ENDIF
: ELSE
:   IF &UC4RB_WAS_APPLICATION_NAME# = ""
:     STOP MSG, 50, "Application Name must be provided for Edit operation."
:   ENDIF
: ENDIF


: DEFINE &STRING_ARR#, string, 30
: INC PCK.ITPA_SHARED.PUB.INCLUDE.SUDOERS_HEADER

!------------------------------------------ LOGGING INPUT PARAMETERS ------------------------------------------
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.HEADER.PARAM_VALIDATION

! Host
: SET &UC4RB_LOGGING_PARAMETER# = "Host"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_WAS_HOST#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM

! Port
: SET &UC4RB_LOGGING_PARAMETER# = "Port"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_WAS_PORT#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM

! Username
: SET &UC4RB_LOGGING_PARAMETER# = "Username"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_WAS_USERNAME#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM

! WSAdmin Tool Bin Directory
: SET &UC4RB_LOGGING_PARAMETER# = "WSAdmin Tool Bin Directory"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4_WSADMIN_DIR_PATH#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM

! File Path	
: SET &UC4RB_LOGGING_PARAMETER# = "File Path"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_WAS_FILE_PATH#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM

! Application Name
: SET &UC4RB_LOGGING_PARAMETER# = "Application Name"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_WAS_APPLICATION_NAME#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM

! Module Operation
: SET &UC4RB_LOGGING_PARAMETER# = "Module Operation"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_WAS_MODULE_OPERATION#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM

! Advanced Options
: SET &UC4RB_LOGGING_PARAMETER# = "Advanced Options"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_WAS_ADV_OPTIONS#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM

! logging start of execution
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.HEADER.EXECUTION

!-------------------------------------------------Forming the command--------------------------------------------
: SET &UC4RB_ESCAPED_STRING# = "&UC4_WSADMIN_DIR_PATH#/wsadmin.sh"
  PATH=$PATH:&UC4_WSADMIN_DIR_PATH#;export PATH
: INC PCK.ITPA_SHARED.PRV.INCLUDE.CHECK_SHELL_CMD@UNIX

: INC PCK.AUTOMIC_IBM_WEBSPHERE.PUB.INCLUDE.ESCAPE_ARG_ADD_QUOTATION

: INCLUDE PCK.AUTOMIC_IBM_WEBSPHERE.PRV.INCLUDE.ATTACH_RES
: SET &UC4RB_WAS_CMD# = "java -jar ""&$AGENT_RESOURCES_CLIENT#itpa-tool.jar"" arb -cmd execute &UC4RB_ESCAPED_STRING#"

! Username
: SET &UC4RB_ESCAPED_STRING# = &UC4RB_WAS_USERNAME#
: INC PCK.AUTOMIC_IBM_WEBSPHERE.PUB.INCLUDE.ESCAPE_ARG_ADD_QUOTATION
: SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#, " -username &UC4RB_ESCAPED_STRING#")

! Password
: SET &UC4RB_ESCAPED_STRING# = &UC4RB_WAS_PASSWORD#
: INC PCK.AUTOMIC_IBM_WEBSPHERE.PUB.INCLUDE.ESCAPE_ARG_ADD_QUOTATION
: SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#, " -password &UC4RB_ESCAPED_STRING#")

: IF &UC4RB_WAS_HOST# <> ""
!     Host
:     SET &UC4RB_ESCAPED_STRING# = &UC4RB_WAS_HOST#
:     INC PCK.AUTOMIC_IBM_WEBSPHERE.PUB.INCLUDE.ESCAPE_ARG_ADD_QUOTATION
:     SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#," -host &UC4RB_ESCAPED_STRING#")

!     Port
:     SET &UC4RB_ESCAPED_STRING# = &UC4RB_WAS_PORT#
:     INC PCK.AUTOMIC_IBM_WEBSPHERE.PUB.INCLUDE.ESCAPE_ARG_ADD_QUOTATION
:     SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#, " -port &UC4RB_ESCAPED_STRING#")
: ENDIF

! Advanced Options	
: IF &UC4RB_WAS_ADV_OPTIONS# <> ""
:   SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#, " &UC4RB_WAS_ADV_OPTIONS#")
: ENDIF

! Attach python file to prepare running
: ATTACH_RES "PCK.AUTOMIC_IBM_WEBSPHERE.PRV.STORE", "MAP_MODULES_TO_SERVERS", T
: SET &FILENAME# = STR_CAT(&$AGENT_RESOURCES_TASK#, "pck_websphere_map_modules_to_servers.py")
: SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#, " -f ""&FILENAME#""")

: SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#, " ""&UC4RB_WAS_WSADMINLIB#""")
: SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#, " ""&UC4RB_WAS_UTILSLIB#""")

! File Path	
: IF &UC4RB_WAS_FILE_PATH# <> ""
:   SET &UC4RB_ESCAPED_STRING# = &UC4RB_WAS_FILE_PATH#
:   INC PCK.AUTOMIC_IBM_WEBSPHERE.PUB.INCLUDE.ESCAPE_ARG_ADD_QUOTATION
:   SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#, " &UC4RB_ESCAPED_STRING#")
: ENDIF

! Application name
: IF &UC4RB_WAS_APPLICATION_NAME# <> ""
:   SET &UC4RB_ESCAPED_STRING# = &UC4RB_WAS_APPLICATION_NAME#
:   INC PCK.AUTOMIC_IBM_WEBSPHERE.PUB.INCLUDE.ESCAPE_ARG_ADD_QUOTATION
:   SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#, " &UC4RB_ESCAPED_STRING#")
: ENDIF

! Modules
!   Get File path
:   SET &AGENT_TEMPDIR# = GET_VAR('UC_EX_PATH_TEMP', &AGENT#)
:   SET &UC4RB_TEMP_FILE_PATH# = STR_CAT(&AGENT_TEMPDIR#,"map_modules_to_servers-&$RUNID#.txt")
:   SET &UC4RB_FILE_CONTENT# = &UC4RB_WAS_MODULES#
!   Creating file in the temp dir
:   SET &UC4RB_FILE_PATH#=&UC4RB_TEMP_FILE_PATH#
:   INC PCK.AUTOMIC_IBM_WEBSPHERE.PRV.INCLUDE.CREATE_FILE

! Process file content
:   SET &HND# = PREP_PROCESS_FILE(&AGENT#,&UC4RB_FILE_PATH#,,,,'UC_LOGIN=&LOGIN#')
:   PROCESS &HND#
:   SET &LINE#=GET_PROCESS_LINE(&HND#)
:   IF &LINE# <> ""
:     SET &UC4RB_MODULES_FILE_CONTENT# = STR_CAT(&UC4RB_MODULES_FILE_CONTENT#, &LINE#)
:     SET &UC4RB_MODULES_FILE_CONTENT# = STR_CAT(&UC4RB_MODULES_FILE_CONTENT#, " ** ")
:   ENDIF
:   ENDPROCESS
:   CLOSE_PROCESS &HND#

:   SET &CONTENT_LENGTH# = STR_LENGTH(&UC4RB_MODULES_FILE_CONTENT#)
:   SET &POS# = &CONTENT_LENGTH#-4
:   SET &UC4RB_MODULES_FILE_CONTENT# = SUBSTR(&UC4RB_MODULES_FILE_CONTENT#,1,&POS#)
:   SET &UC4RB_ESCAPED_STRING# = &UC4RB_MODULES_FILE_CONTENT#
:   INC PCK.AUTOMIC_IBM_WEBSPHERE.PUB.INCLUDE.ESCAPE_ARG_ADD_QUOTATION
:   SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#, " &UC4RB_ESCAPED_STRING#")

! Module Operation
: SET &UC4RB_ESCAPED_STRING# = &UC4RB_WAS_MODULE_OPERATION#
: INC PCK.AUTOMIC_IBM_WEBSPHERE.PUB.INCLUDE.ESCAPE_ARG_ADD_QUOTATION
: SET &UC4RB_WAS_CMD# = STR_CAT(&UC4RB_WAS_CMD#, " &UC4RB_ESCAPED_STRING#")

! logging the command
: PRINT &UC4RB_WAS_CMD#

! Invoking java binary
  &UC4RB_WAS_CMD#

:PUBLISH &UC4RB_TEMP_FILE_PATH#,,"TASK"

! Status messages
: SET &UC4RB_LOGGING_INFO# = "******** MAP MODULES TO SERVERS ACTION EXECUTED SUCCESSFULLY ********"
: SET &UC4RB_LOGGING_ERROR# = "******** MAP MODULES TO SERVERS ACTION FAILED ********"

!------------------------------------------ Error Handling -----------------------------------------------------------------------------------------------------

: INC PCK.AUTOMIC_IBM_WEBSPHERE.PRV.INCLUDE.CHECK_JOBSTATUS
: INC PCK.ITPA_SHARED.PUB.INCLUDE.SUDOERS_FOOTER]]></process>
      </row>
      <row>
         <pre_process>:INC PCK.ITPA_SHARED.PUB.INCLUDE.START_STOP_JOB_ACCORDING_TO_AGENT_OS</pre_process>
      </row>
      <row>
         <post_process><![CDATA[:  PUT_PROMPT_BUFFER AGENT# = &AGENT#
:  PUT_PROMPT_BUFFER LOGIN# = &LOGIN#
:  PUT_PROMPT_BUFFER  UC4RB_DIR_FILE_RM_FAIL# = "YES"
:  PUT_PROMPT_BUFFER UC4RB_DIR_FILE_RM_NAME# = &UC4RB_TEMP_FILE_PATH#
:  SET &UC4RB_WEBSPHERE_OBJ# = "PCK.AUTOMIC_FILESYSTEM.PUB.ACTION.DELETE"
:  SET &ACTOBJ# = ACTIVATE_UC_OBJECT(&UC4RB_WEBSPHERE_OBJ#,WAIT)
:  IF &ACTOBJ# = "0"
:    SET &ERRNR# = SYS_LAST_ERR_NR()
:    SET &ERRINS#  = SYS_LAST_ERR_INS()
:    SET &MESSAGE# = GET_MSG_TXT(&ERRNR#,&ERRINS#)
:    PRINT "Error while activating &UC4RB_WEBSPHERE_OBJ# object :&MESSAGE#"
:  ELSE
:    SET &STATUS# = GET_UC_OBJECT_STATUS(,&ACTOBJ#,"RETCODE")
:    P "EXECUTED OBJECT [&UC4RB_WEBSPHERE_OBJ#] STATUS: &STATUS#"
:    IF &STATUS# <> "0"
:      PRINT "Error while executing &UC4RB_WEBSPHERE_OBJ# object"
:    ENDIF
:  ENDIF]]></post_process>
      </row>
   </scripts>
   <job_attributes>
      <row>
         <platform>UNIX</platform>
         <agent><![CDATA[<UNIX>]]></agent>
         <job_report_path>2</job_report_path>
         <priority>0</priority>
         <unix_shell>-bash</unix_shell>
         <unix_shell_options></unix_shell_options>
         <unix_type></unix_type>
         <unix_cmd></unix_cmd>
      </row>
   </job_attributes>
   <rollback_definitions>
      <row>
      </row>
   </rollback_definitions>
</jobs>
