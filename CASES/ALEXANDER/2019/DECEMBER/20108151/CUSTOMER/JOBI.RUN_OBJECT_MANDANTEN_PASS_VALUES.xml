<?xml version="1.0" encoding="UTF-8"?>
<jobi>
   <metadata>
      <row>
         <version>12.3.1</version>
      </row>
   </metadata>
   <general_attributes>
      <row>
         <minimum_ae_version>11.2</minimum_ae_version>
         <archive_key1>BBS</archive_key1>
         <archive_key2>ALLGEMEIN</archive_key2>
         <child_flags>00000000000000000000000000000000</child_flags>
         <last_runtimes>AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA</last_runtimes>
         <name>JOBI.RUN_OBJECT_MANDANTEN_PASS_VALUES</name>
         <type>JOBI</type>
         <description>Start von &amp;RUN_OBJECT# fuer alle in &amp;MANDANTEN# enthaltenen Mandanten, mit dem Parameter PASS_VALUES ...</description>
         <versioning_id>-202325800</versioning_id>
      </row>
   </general_attributes>
   <scripts>
      <row>
         <process><![CDATA[:PRINT ""
:PRINT "[START JOBI.RUN_OBJECT_MANDANTEN_PASS_VALUES]==========="
! ------------------------------------------------------------------------
! Start von &RUN_OBJECT# fuer jeden in &MANDANTEN# hinterlegten Mandanten:
! ------------------------------------------------------------------------
! Folgende Parameter finden hier Verwendung und muessen im Vorfeld defi-
! niert worden sein:
! ------------------------------------------------------------------------
! &RUN_OBJECT# -> zu startendes Objekt
! &MANDANTEN#  -> Vektor mit den zu berücksichtigenden Mandanten
! &TS#         -> Timestamp
! ------------------------------------------------------------------------
! Alle vorhandenen Variablen werden als Objektvariablen an das zu startende
! Objekt uebergeben. Aus diesem Grund ist es unbedingt erforderlich, dass
! das in der Variablen &RUN_OBJECT# hinterlegte Objekt nicht zur Laufzeit
! generiert wird.
! ------------------------------------------------------------------------
! Ueber Kopie des Mandanten-Vektors iterieren:
! --------------------------------------------
:SET &MANDANTEN_REP# =&MANDANTEN#
:SET &DELIM# = ','
:PRINT ""
:SET &STOP# = 0
:WHILE &STOP# = 0
:  PRINT ">>> Noch zu verarbeitende Mandanten: &MANDANTEN_REP#"
:  SET &POS# = STR_FIND(&MANDANTEN_REP#, &DELIM#)
:  IF &POS# = 0
:    PSET &MANDANT# = &MANDANTEN_REP#
:    SET &STOP# = 1
:  ELSE
:    SET &POSM# = SUB(&POS#, 1)
:    SET &POSP# = ADD(&POS#, 1)
:    SET &MANDANT# = STR_CUT(&MANDANTEN_REP#, 1, &POSM#)
:    PSET &MANDANT# = FORMAT("&MANDANT#", "00")
:    SET &MANDANTEN_REP# = STR_CUT(&MANDANTEN_REP#, &POSP#)
:  ENDIF
! ----------------------------------
! Bei Fehler nicht gleich abbrechen:
! ----------------------------------
:  ON_ERROR RESUME
! -----------
! Los geht's:
! -----------
:  SET &RUN_OBJECT_ALIAS# = "&RUN_OBJECT#_MD&MANDANT#"
:  SET &AKTJOB# = ACTIVATE_UC_OBJECT(&RUN_OBJECT#, , , , , PASS_VALUES, , &RUN_OBJECT_ALIAS#)
! ---------------------
! Activate erfolgreich?
! ---------------------
:  IF &AKTJOB# <> 0
:    PRINT " -> Die Aufgabe &RUN_OBJECT# konnte für"
:    PRINT "    MANDANT: &MANDANT#, TIMESTAMP: &TS#"
:    PRINT "    erfolgreich gestartet werden!"
:    PRINT ""
:  ELSE
! ------------------
! Details ermitteln:
! ------------------
:    SET &ERRNR# = SYS_LAST_ERR_NR()
:    SET &ERRINS# = SYS_LAST_ERR_INS()
:    SET &MESSAGE# = GET_MSG_TXT(&ERRNR#, &ERRINS#)
:    PRINT " -> Die Aufgabe &RUN_OBJECT# konnte für"
:    PRINT "    MANDANT: &MANDANT#, TIMESTAMP: &TS#"
:    PRINT "    nicht gestartet werden!"
:    PRINT " -> Details:"
:    PRINT "    &MESSAGE#"
:    STOP MSG, 50, "-> &RUN_OBJECT# konnte nicht gestartet werden!"
:  ENDIF
:ENDWHILE
:PRINT "============[ENDE JOBI.RUN_OBJECT_MANDANTEN_PASS_VALUES]"
:PRINT ""]]></process>
      </row>
   </scripts>
</jobi>
